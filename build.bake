import System
import System.IO
import System.Xml from "C:\\Windows\\Microsoft.NET\\Framework\\v2.0.50727\\System.Xml.dll"
import System.Security.Cryptography.X509Certificates
import System.Net.Mail
import System.Threading
import Microsoft.Build.Framework
import Microsoft.Build.Tasks from "Microsoft.Build.Tasks.v3.5"
import Microsoft.Build.Utilities.TaskItem from "Microsoft.Build.Utilities.v3.5" as TaskItem
import ICSharpCode.SharpZipLib.Zip
import Bake.Win32.Helper

class BakeMsbuildEngine(IBuildEngine):

	ContinueOnError as bool:
		get:
			return false

	ProjectFileOfTaskNode as string:
		get:
			return ""

	LineNumberOfTaskNode as int:
		get:
			return 0

	ColumnNumberOfTaskNode as int:
		get:
			return 0

	def LogErrorEvent(a as BuildErrorEventArgs):
		print a.Message

	def LogWarningEvent(a as BuildWarningEventArgs):
		print a.Message

	def LogMessageEvent(a as BuildMessageEventArgs):
		print a.Message

def Clean(path as String):
	if not Exist(path):
		MkDir(path)
	else:
		Rm("${path}*", true)

def GeneratePublishPage(file as string, version as string, projectName as string):
	template = System.IO.File.ReadAllText("publish.htm.template")
	result = template.Replace("{version}", version)
	result = result.Replace("{name}", projectName)
	System.IO.File.AppendAllText(file, result)

project = "ProducerEditor"
buildRoot = "output"
projectFile = Path.GetFullPath("src/${project}/${project}.csproj")
output = Path.GetFullPath("$buildRoot/${project}");
deploymentUrl = "http://publish.analit.net/ProducerEditor/"
publisher = "Inforoom"

Global(
	Project : "ProducerEditor",
	HumanReadableName : "Редактор производителей",
	Profile: @catalog,
	GenerateAssemblyInfoIgnore: (/DevAge.+/,),
	TestIgnore: (/Subway.+/,)
)

def Clean(path as String):
	if not Exist(path):
		MkDir(path)
	else:
		Rm("${path}*", true)

task @default, [@buildDeployPackage]

task @build, [@clean]:
	MsBuild(projectFile,
			Parameters : { "OutDir" : output + "\\" ,
						   "Configuration" : "release"},
			FrameworkVersion : "4.0.30319").Execute()
	Cp("src/${project}/App.release.config", "${output}/${project}.exe.config", true)
	Rm(FileSet("*.*",
			   BaseDirectory : output,
			   Excludes : ["*.dll", "*.exe", "*.config"]))
task @clean:
	MsBuild(projectFile,
			Target : "clean",
			Parameters : { "Configuration" : "release" },
			FrameworkVersion : "4.0.30319").Execute()
	Clean(output)

task @buildService, [@cleanService]:
	MsBuild(Path.GetFullPath("src/ProducerEditor.Service/ProducerEditor.Service.csproj"),
			Parameters : { "OutDir" : Path.GetFullPath("$buildRoot/ProducerEditor.Service/bin") + "\\" ,
						   "Configuration" : "release"},
			FrameworkVersion : "4.0.30319").Execute()
	Cp("src/ProducerEditor.Service/Web.release.config", "$buildRoot/ProducerEditor.Service/Web.config", true)
	Rm(FileSet("*.*",
			   BaseDirectory : "$buildRoot/ProducerEditor.Service/bin",
			   Excludes : ["*.dll", "*.exe", "*.config"]))
	Cp(FileSet(["**/*.svc", "**/*.asax"], BaseDirectory : "src/ProducerEditor.Service/"), "$buildRoot/ProducerEditor.Service/")

task @cleanService:
	MsBuild("src/ProducerEditor.Service/ProducerEditor.Service.csproj",
			Target : @clean,
			Parameters : { @Configuration : @release },
			FrameworkVersion : "4.0.30319").Execute()
	Clean("$buildRoot/ProducerEditor.Service")

task @deployService, [@buildService, @cleanServiceDeployDir, "deploy:notify"]:
	Cp(FileSet("**/*.*", BaseDirectory : "$buildRoot/ProducerEditor.Service/"), "\\\\acdcserv\\WebApps\\ProducerEditor.Service\\")

task @cleanServiceDeployDir:
	Clean("\\\\acdcserv\\WebApps\\ProducerEditor.Service\\")

task @buildDeployPackage, [@GenerateAssemblyInfo, @build, @buildService, @buildInstaller, @cleanDeployPackage]:
	version = GetVersion()
	output = Path.GetFullPath("$buildRoot/${project}.Setup/")
	source = Path.GetFullPath("$buildRoot/${project}/")
	certPath = GetResource("inforoomCS.pfx")
	password = "password"

	items = (TaskItem("${project}", {"Visible" : "false", "Install" : "true"}),
			TaskItem(".NETFramework,Version=v4.0,Profile=Client", {"Visible" : "false", "Install" : "true"}),
			TaskItem("Microsoft.Windows.Installer.3.1", {"Visible" : "false", "Install" : "true"}))
	bootstrapper = Path.GetFullPath("$buildRoot/bootstrapper/")

	Cp(FileSet("**/*.*", BaseDirectory : Path.GetFullPath("bootstrapper"), Excludes : ["**/.svn/**"]), bootstrapper, true)

	installer = "$buildRoot/Installer/"
	Cp(FileSet("*.*", BaseDirectory : installer), "$buildRoot/bootstrapper/Packages/${project}")

	config = XmlDocument()
	config.Load("${source}/${project}.exe.config")
	namespaceManager = XmlNamespaceManager(config.NameTable)
	namespaceManager.AddNamespace("n", "urn:schemas-microsoft-com:asm.v1");
	config.SelectSingleNode("configuration/runtime/n:assemblyBinding/n:probing", namespaceManager).Attributes["privatePath"].Value = version
	config.Save("${source}/${project}.exe.config")

	FastZip().CreateZip(Path.Combine(Path.GetFullPath("$buildRoot/bootstrapper/Packages/${project}"), "${project}.zip"), source, false, null)

	updateFiles = ""
	packageFiles = ""
	for file in FileSet("*.*", BaseDirectory : "$buildRoot/bootstrapper/Packages/${project}", Excludes : ["product.xml"]).Files:
		filename = Path.GetFileName(file)
		updateFiles += "<file>${deploymentUrl}${project}/${filename}</file>"
		packageFiles += "<PackageFile Name=\"${filename}\" />"

	product = XmlDocument()
	product.Load("$buildRoot/bootstrapper/Packages/${project}/product.xml")
	namespaceManager = XmlNamespaceManager(product.NameTable)
	namespaceManager.AddNamespace("n", "http://schemas.microsoft.com/developer/2004/01/bootstrapper");
	product.SelectSingleNode("//n:Product/n:PackageFiles", namespaceManager).InnerXml = packageFiles
	product.Save("$buildRoot/bootstrapper/Packages/${project}/product.xml")

	config = XmlDocument()
	config.Load("$buildRoot/bootstrapper/Packages/${project}/Installer.exe.config")
	config.SelectSingleNode("configuration/appSettings/add[@key=\"Application\"]").Attributes["value"].Value = project
	config.SelectSingleNode("configuration/appSettings/add[@key=\"Publisher\"]").Attributes["value"].Value = publisher
	config.SelectSingleNode("configuration/appSettings/add[@key=\"Version\"]").Attributes["value"].Value = version
	config.SelectSingleNode("configuration/appSettings/add[@key=\"UpdateUri\"]").Attributes["value"].Value = Path.Combine(deploymentUrl, "update.xml")
	config.Save("$buildRoot/bootstrapper/Packages/${project}/Installer.exe.config")

	update = XmlDocument()
	update.InnerXml = """<?xml version="1.0" encoding="utf-8"?>
<update>
	<version>${version}</version>
	<files>
		${updateFiles}
	</files>
</update>"""
	update.Save("$buildRoot/${project}.Setup/update.xml")

	GenerateBootstrapper(ApplicationName : project,
						ApplicationRequiresElevation : false,
						OutputPath : output,
						Path : bootstrapper,
						BootstrapperItems : items,
						BuildEngine: BakeMsbuildEngine(),
						#ComponentsLocation: "HomeSite",
						#для .net 4 установка зависимостей из папки с приложением не работает
						ComponentsLocation : "Absolute",
						ComponentsUrl : deploymentUrl,
						CopyComponents : true,
						Culture : "ru").Execute()
	Mv("${output}setup.exe", "${output}${project}.Setup.exe")

	cert = X509Certificate2(System.IO.File.ReadAllBytes(certPath), password)
	Sh("\"signtool\" sign /sha1 ${cert.Thumbprint} ${output}${project}.Setup.exe")

	GeneratePublishPage("${output}/publish.htm", version, project)

task @buildInstaller:
	projectFile = Path.GetFullPath("src/Installer/Installer.csproj")
	output = Path.GetFullPath("$buildRoot/Installer/")
	MsBuild(projectFile,
			Parameters : { "OutDir" : output,
						   "Configuration" : "release" },
			FrameworkVersion : "4.0.30319").Execute()
	Rm(FileSet("*.*", Excludes : ["*.dll", "*.exe", "*.config"], BaseDirectory : output))

task @cleanDeployPackage:
	Clean("$buildRoot/${project}.Setup")
	Clean("$buildRoot/bootstrapper")

task @deploy, ["check", @UpdateRevision, @buildDeployPackage, @Production, @Migrate, @cleanDeployDir, @deployService, "deploy:notify"]:
	deployDir = Path.Combine(Globals.PublishRoot, project)
	Cp(FileSet("**/*.*", BaseDirectory : "$buildRoot/${project}.Setup"), deployDir)

task @cleanDeployDir:
	path = Path.Combine(Globals.PublishRoot, project)
	if not Exist(path):
		MkDir(path)
	else:
		Rm(FileSet("**/*.*", BaseDirectory : path, Excludes : ["**/web.config"]), true)
		for dir in Directory.GetDirectories(path):
			Directory.Delete(dir, true)
